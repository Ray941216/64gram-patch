pkgname=64gram-desktop-modded
_pkgname=64Gram
pkgver=1.1.0
pkgrel=1
epoch=1
pkgdesc='Unofficial desktop version of Telegram messaging app'
arch=('x86_64')
url="https://github.com/TDesktop-x64/tdesktop"
license=('GPL3')
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal' 'ttf-opensans'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'xxhash'
         'rnnoise' 'pipewire' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch'
         'openssl' 'protobuf' 'glib2' 'libsigc++-3.0')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'libtg_owt'
             'gobject-introspection' 'boost' 'fmt' 'mm-common' 'perl-xml-parser' 'libsigc++-3.0')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
provides=(telegram-desktop)
conflicts=(telegram-desktop)
_commit="tag=v$pkgver"
source=("disable-anti-forward.patch"
	"io.github.tdesktop_x64.TDesktop.desktop"
        "https://download.gnome.org/sources/glibmm/2.77/glibmm-2.77.0.tar.xz"
)
sha512sums=('00fb74b0255d09a68110eaa4c46930b8137ac744918f764208d98792b00b8518c12b3573573e97a5975d139dfa9ccfc4a456405bb514baf50f25a4e18e29e2f7'
            'a2a602fb97f4db649e2b937c1081d4c3c62a0ad558f1d3171744c9d731bcbb911f6e8dab14fd399c8e7182557d2bf7fcd0c263a4beac69d9060c3a9e1ac664f1'
            '6650e822de2529582d93291025500afb6a182a0c5a564f656f164d79d8765bb4ca9c9d16227148431cc71c2677923b9364e81bbd4ca4f07f68e36bb380fb9574')

prepare() {
    git clone --branch=v$pkgver --depth=1 --single-branch --recurse-submodules --shallow-submodules https://github.com/TDesktop-x64/tdesktop.git

    cd tdesktop
    
    # Patch here, if needed!
    # patch -Np1 -i "$srcdir/my_beautiful.patch"
    patch -Np1 -i "$srcdir/disable-anti-forward.patch"

    # Official package patches
}

build() {
    CXXFLAGS+=' -ffat-lto-objects'

    meson setup -D maintainer-mode=true --default-library static --prefix "$srcdir/glibmm" glibmm-2.77.0 glibmm-build
    meson compile -C glibmm-build $MAKEFLAGS
    meson install -C glibmm-build

    export PKG_CONFIG_PATH="$srcdir"/glibmm/usr/local/lib/pkgconfig
    cmake -B build -S tdesktop -G Ninja \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_PREFIX_PATH="$srcdir/glibmm" \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=611335 \
        -DTDESKTOP_API_HASH=d524b414d21f4d37f08684c1df41ac9c \
	-DCMAKE_CXX_COMPILER_LAUNCHER=ccache
    cmake --build build -- $MAKEFLAGS
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 /dev/null "$pkgdir/etc/tdesktop/externalupdater"
    install -Dm644 "$srcdir/io.github.tdesktop_x64.TDesktop.desktop" "$pkgdir/usr/share/applications/io.github.tdesktop_x64.TDesktop.desktop"
}
